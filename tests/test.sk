# Meta testing stuff
let testTotal = 0;
let testBad = 0;
func test(msg, val, goodVal) {
    let str = "";
    testTotal += 1;
    if val == goodVal {
        str = "works: ";
    } else {
        str = "FAIL: ";
        testBad += 1;
    }
    str += msg + ": ";
    print(str + goodVal + " == " + val);
}

func testSummary() {
    print("");
    print("SUMMARY: " + (testTotal-testBad) + " out of " + testTotal + " tests passed");
    if testBad == 0 {
        print("ALL TESTS PASSED!");
    }
}

# Math ops tests
print("*** Mathematical operators tests ***");
test("2 + 3", 2 + 3, 5);
test("2 + 3 + 4", 2 + 3 + 4, 9);

test("2 - 3", 2 - 3, -1);
test("2 - 3 - 4", 2 - 3 - 4, -5);

test("2 * 3", 2 * 3, 6);
test("2 * 3 * 4", 2 * 3 * 4, 24);

test("6 / 3", 6 / 3, 2.0);
test("42 / 3 / 2", 42 / 3 / 2, 7.0);

test("2 % 3", 2 % 3, 2);
test("15 % 4", 15 % 4, 3);

# Cmp tests
print("*** Comparison tests ***");
test("2 > 2", 2 > 2, false);
test("3 > 1", 3 > 1, true);
test("1 > 3", 1 > 3, false);

test("2 < 2", 2 < 2, false);
test("3 < 1", 3 < 1, false);
test("1 < 3", 1 < 3, true);

test("2 >= 2", 2 >= 2, true);
test("3 >= 1", 3 >= 1, true);
test("1 >= 3", 1 >= 3, false);

test("2 <= 2", 2 <= 2, true);
test("3 <= 1", 3 <= 1, false);
test("1 <= 3", 1 <= 3, true);

test("2 == 2", 2 == 2, true);
test("3 == 1", 3 == 1, false);
test("1 == 3", 1 == 3, false);
test('"E" == "E"', "E" == "E", true);
test('"E" == "h"', "E" == "h", false);

test("2 != 2", 2 != 2, false);
test("3 != 1", 3 != 1, true);
test("1 != 3", 1 != 3, true);
test('"E" != "E"', "E" != "E", false);
test('"E" != "h"', "E" != "h", true);

print("*** Boolean operator tests ***");
test("0 & 0", false && false, false);
test("1 & 0", true && false, false);
test("1 & 1", true && true, true);

test("0 | 0", false || false, false);
test("1 | 0", true || false, true);
test("1 | 1", true || true, true);

test("0 ^ 0", false ^^ false, false);
test("1 ^ 0", true ^^ false, true);
test("1 ^ 1", true ^^ true, false);

test("!0", !false, true);
test("!1", !true, false);

# Add test
print("*** Adding tests ***");
func add_things(a, b) {
    let c = a + b;
    return c;
}
test("2 + 3", add_things(2, 3), 5);
test("-9 + 19", add_things(-9, 19), 10);
test("1.3 + 0.19", add_things(1.3, 0.19), 1.49);
test('"ab" + "c"', add_things("ab", "c"), "abc");
test('"X=" + 42', add_things("X=", 42), "X=42");

# Setting operators
print("*** Operator tests ***");
let x = 1;
test("let", x, 1);
x -= 1;
test("-=", x, 0);
x = 4;
test("=", x, 4);
x /= 2;
test("/=", x, 2.0);
x += 1;
test("+=", x, 3.0);
x *= 2;
test("*=", x, 6.0);
x = 1 - 1 - 1;
test("associativity", x, -1);

# Type tests
print("*** Type tests ***");
test("String", type("Hello"), "String");
test("Number", type(0), "Number");
test("Decimal", type(0.0), "Decimal");
test("Bool", type(false), "Bool");
test("None", type(none), "None");

# Factoral
print("*** Factoral tests ***");
func factorial(n) {
    if n < 0 {
        # factorial(x) is undefined when x < 0
        return none;
    }
    if n < 2 {
        return 1;
    }
    return n * factorial(n - 1);
}
test("factorial(4)", factorial(4), 24);
test("factorial(0)", factorial(0), 1);
test("factorial(-17)", factorial(-17), none);

# String tests
print("*** String tests ***");
let str = "Hello";
test("let str", str, "Hello");
str += " World";
test("str+", str, "Hello World");
str = "E";
test("str=", str, "E");
str *= 3;
test("str*", str, "EEE");

# Iter loop tests
print("*** Iter loop tests ***");
let i = 0;
loop (j in range(1, 10)) {
    ++i;
    test("i == j", i, j);
}
test("i == 10", i, 10);

# While loop test
print("*** While loop tests ***");
i = 1;
loop (while i < 6) {
    test("i < 6", i < 6, true);
    ++i;
}
test("i == 6", i, 6);

# Import test
print("*** Import tests ***");
import ("import-test.sk");
test("import func", ret4(), 4);
test("import var (extension)", myCoolVar, 7);

# Casting tests
print("*** Casting tests ***");
test("int -> int", int(42), 42);
test("int -> float", float(42), 42.0);
test("int -> str", string(0), "0");

test("float -> int", int(4.2), 4);
test("float -> float", float(4.2), 4.2);
test("float -> str", string(4.2), "4.2");

test("str -> int", int("42"), 42);
test("str -> float", float("4.2"), 4.2);
test("str -> str", string("E"), "E");

test("bool -> int", int(true), 1);
test("bool -> float", float(true), 1.0);
test("bool -> str", string(true), "true");

test("none -> int", int(none), 0);
test("none -> float", float(none), 0.0);
test("none -> str", string(none), "none");

# List tests
print("*** List tests ***");
let elm = 4;
let list = [1, "E>h", elm, x: 42, elm * 2];
test("list -> str", string(list), '[1, E>h, elm: 4, x: 42, 8]');
test("len()", len(list), 5);
test("number indexing", list[2], 4);
test("key indexing", list['elm'], 4);
test("var indexing", list[elm], 8);

# If tests
print("*** If tests ***");
let if_test = false;
if 1 {
    if_test = true;
}
test("if 1", if_test, true);

if 0 {
    if_test = false;
}
test("if 0", if_test, true);

if 1 {
    test("if 1 else", 1, 1);
} else {
    test("if 1 else", 1, 0);
}

if 0 {
    test("if 0 else", 1, 0);
} else {
    test("if 0 else", 1, 1);
}

# End of tests
testSummary();
